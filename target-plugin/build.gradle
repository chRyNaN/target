import com.chrynan.target.buildSrc.LibraryConstants
import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact

apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "java-gradle-plugin"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

group LibraryConstants.group
version LibraryConstants.versionName

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    compileOnly(gradleApi())

    implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"

    implementation project(":target-core")
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = project.findProperty("BINTRAY_USER") ?: System.getenv("BINTRAY_USER")
    key = project.findProperty("BINTRAY_KEY") ?: System.getenv("BINTRAY_KEY")

    pkg {
        repo = LibraryConstants.bintrayRepo
        name = LibraryConstants.repoName
        licenses = [LibraryConstants.license]
        vcsUrl = LibraryConstants.vcsUrl

        version {
            name = LibraryConstants.versionName
            desc = LibraryConstants.versionDescription
            released = new Date()
            vcsTag = LibraryConstants.versionName
        }
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect { publication ->
        File moduleFile = new File(project.buildDir, "publications/${publication.name}/module.json")

        if (moduleFile.exists()) {
            publication.artifact(new FileBasedMavenArtifact(moduleFile) {
                protected String getDefaultExtension() {
                    return "module"
                }
            })
        }

        publication.name
    }
}

bintrayUpload.dependsOn publishToMavenLocal

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = LibraryConstants.group
            artifactId = "target-plugin"
            version = LibraryConstants.versionName

            from components.java
        }
    }
}

gradlePlugin {
    plugins {
        jsonStringInputPlugin {
            id = "com.chrynan.target.plugin"
            implementationClass = "com.chrynan.target.plugin.TargetPlugin"
        }
    }
}
